#!/usr/bin/env spang2

# @title get children of a taxon with numbers of species
# @endpoint test
# @param taxid=40674

SELECT ?order ?order_name ?count_order ?family ?family_name ?count_family ?species ?name
WHERE {
  ?species taxon:rank taxon:Species ;
      rdfs:label ?name ;
      rdfs:subClassOf+ taxid:{{taxid}} ;
      rdfs:subClassOf+ ?family ;
      rdfs:subClassOf+ ?order .
  ?family taxon:rank taxon:Family ;
      rdfs:label ?family_name .
  ?order taxon:rank taxon:Order ;
      rdfs:label ?order_name .
  {
    SELECT (COUNT(DISTINCT ?species) AS ?count_order) ?order
    WHERE {
      ?species taxon:rank taxon:Species ;
          rdfs:subClassOf+ ?order .
    }
  }
  {
    SELECT (COUNT(DISTINCT ?species) AS ?count_family) ?family
    WHERE {
      ?species taxon:rank taxon:Species ;
          rdfs:subClassOf+ ?family .
    }
  }
}
ORDER BY DESC(?count_order) ?order_name DESC(?count_family) ?family_name ?name
